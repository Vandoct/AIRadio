apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def getPassword(String currentUser, String keyChain) {
    def stdout = new ByteArrayOutputStream()
    def stderr = new ByteArrayOutputStream()
    exec {
        commandLine 'security', '-q', 'find-generic-password', '-a', currentUser, '-s', keyChain, '-w'
        standardOutput = stdout
        errorOutput = stderr
        ignoreExitValue = true
    }

    stdout.toString().trim()
}

def passKeyStore = getPassword("Ichvandi", "android_keystore")
def passKey = getPassword("Ichvandi", "android_key")

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.0"
    defaultConfig {
        applicationId "id.antenaislam.airadio"
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "BASE_URL", '"https://radio.antenaislam.com/"')
        resConfigs "en"
    }
    signingConfigs {
        release {
            storeFile file(AIRADIO_RELEASE_STORE_FILE)
            storePassword passKeyStore
            keyAlias AIRADIO_RELEASE_KEY_ALIAS
            keyPassword passKey
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "AIRadio-${variant.versionName}.apk"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    def lifecycle_version = "2.0.0"

    // Android
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    // Card view
    implementation 'androidx.cardview:cardview:1.0.0'

    // Recycler view
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'

    // GSON
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

    // Circle image view
    implementation 'de.hdodenhof:circleimageview:3.0.0'

    // Exoplayer
    implementation 'com.google.android.exoplayer:exoplayer:2.7.3'

    // Anko
    implementation "org.jetbrains.anko:anko-commons:0.10.8"

}
